#!/usr/bin/perl -w

use strict;

sub usage {
#################################3
# P: quit message (optional)
#

  my (@messages) = @_;
  print <<__HERE

<program> meshname.def

where par file where each line is blank, or
  
name = value 

End of line comments start with a #

Here are the options that the program will look for:

tissue = 300 400 500 # dimensions along the x,y,and Z axes (in um)
h = 50 # step size along each axis (in um)
fiber = 0 0 1 # Fiber direction, must be normalized to 1
bath = 100 100 100 # extend the tissue by bath_dim along each axis
                   # |<-bath_x->|<---dim_x--->|<-bath_x->|
                   # if omitted, 0 0 0 is assumed


Given a parameter file like the above, the code will generate a grid
(surrounded by an optional bath), complete with .pts, .tetras, and .tri 
files.  

Enjoy!

__HERE
    ;

  die join("\n", @messages) if (@messages); 

}


sub swap(\$\$) {
###########################################
# swaps two scalars.

  my ($a, $b) = @_;
  my $temp;

  $temp = $$a;
  $$a = $$b;
  $$b = $temp;
}

my $error_msg = "Need to specify parameter file meshname.def on the command line";
usage($error_msg) if not @ARGV;

my $par_file = shift @ARGV;
usage($error_msg) if $par_file !~ m,^(.*)\.def$,;
my $name = $1; 

my %par = read_par_file($par_file);

my @tissue = make_list(assert_there('tissue'));
my @fiber = make_list(assert_there('fiber'));
my $h = assert_there('h');
my @bath = (0,0,0);
if (defined($par{bath})) {
    @bath = make_list($par{bath});
}


my @count;
my @start;
my @bath_count;
  
for (my $i=0; $i<3; $i++) {
  my $length = 2*$bath[$i]+$tissue[$i];
  $start[$i] = -$length/2;
  $count[$i] = int($length/$h);
  $bath_count[$i] = int($bath[$i]/$h);
}

{
  #write the points file
  open(PTS, ">$name.pts") or die "Could not open $name.pts: $!\n";
  print PTS ($count[0]+1)*($count[1]+1)*($count[2]+1) . "\n";

  for (my $k=0; $k<=$count[2]; $k++) {
    for (my $j=0; $j<=$count[1]; $j++) {
      for (my $i=0; $i<=$count[0]; $i++) {
	print PTS join(" ", ($i*$h+$start[0], $j*$h+$start[1], $k*$h+$start[2])) . "\n";
      }
    }
  }
  close(PTS);

  #make all the hexahedral elements.
  open(ELEM, ">$name.elem") or die "Could not open $name.elem: $!\n";
  print ELEM $count[0]*$count[1]*$count[2] . "\n";
  for (my $z=0; $z<$count[2]; $z++) {
    for (my $y=0; $y<$count[1]; $y++) {
      for (my $x=0; $x<$count[0]; $x++) {
	print ELEM join(" ",
			(
			 "Hx",
			 get_point($x  , $y  , $z  ),
			 get_point($x+1, $y  , $z  ),
			 get_point($x+1, $y+1, $z  ),
			 get_point($x  , $y+1, $z  ),
			 get_point($x  , $y  , $z+1),
			 get_point($x  , $y+1, $z+1),
			 get_point($x+1, $y+1, $z+1),
			 get_point($x+1, $y  , $z+1),
			 region_number($x, $y, $z)
			)
	    ) ."\n";
      }
    }
  }
  close(ELEM);

  {
    my $num_fibers = $count[0]-2*$bath_count[0];
    $num_fibers *= $count[1]-2*$bath_count[1];
    $num_fibers *= $count[2]-2*$bath_count[2];
    my $fiber_len = sqrt($fiber[0]**2 + $fiber[1]**2 + $fiber[2]**2);
    foreach my $dir (@fiber) {
      $dir /= $fiber_len;
    }
    open(LON, ">$name.lon") or die "Could not open $name.lon: $!\n";
    print LON "$num_fibers\n";
    print LON (join(" ", @fiber)."\n") x $num_fibers;
    close(LON);
  }

  #make all tri files
  open(TRI, ">$name.tri") or die "Could not open $name.tri: $!\n";
  for (my $perp=0; $perp<3; $perp++) {
    my $u=0;
    $u++ if $perp==$u;
    my $v=$u+1;
    $v++ if $perp==$v;

    my $should_swap = 0;
    foreach my $side ($bath_count[$perp], $count[$perp]-$bath_count[$perp]) {
      for (my $x=$bath_count[$u]; $x<$count[$u]-$bath_count[$u]; $x++) {
	for (my $y=$bath_count[$v]; $y<$count[$v]-$bath_count[$v]; $y++) {
	  my @base;
	  $base[$perp] = $side;
	  $base[$u] = $x;
	  $base[$v] = $y;
	  
	  my @opposite = @base;
	  $opposite[$u]++;
	  $opposite[$v]++;

	  foreach my $w ($u, $v) {
	    my @corner = @base;
	    $corner[$w]++;
	    
	    my ($second, $third) = (\@opposite, \@corner);
	    if ($should_swap) {
	      swap($second, $third);
	    }

	    print TRI join(" ",
			   (
			    get_point(@base),
			    get_point(@$second),
			    get_point(@$third)
			    )
			   ) . "\n";

	    $should_swap = not $should_swap;
	  }
	}
      }
      $should_swap = not $should_swap;
    }
  }
  close(TRI);

}

sub region_number {
#########################################
#
#

  my (@coord) = @_;

  for (my $i=0; $i<3; $i++) {
    return 1 if $coord[$i] <  $bath_count[$i];
    return 1 if $coord[$i] >= $count[$i]-$bath_count[$i];
  }

  return 0;

}

sub get_point {
##########################################
# P: coord
# R: index in pts file
  
  my (@coord) = @_;
  
  return $coord[0]+($count[0]+1)*($coord[1]+($count[1]+1)*$coord[2]);
}

sub assert_there {
##########################################
# returns true if a value is defined
  my ($name) = @_;

  usage("Error: name '$name' does not exist in the par file!") if not $par{$name};
  return $par{$name};
}

sub make_list {
#######################################
# P: text
# R: list where each item is separated by a space

  my ($text) = @_;

  return split(/\s+/, strip($text));
}

sub read_par_file {
##########################################3
# P: $par_file
#  file to open
# R: hash containing par file

  my ($par_file) = @_;

  open(FILE, $par_file) or usage("Canot open par file $par_file: $!\n");

  my %ret_hash;
  foreach my $line (<FILE>) {
    $line =~ s,#.*,,;
    $line = strip($line);
    
    next if not $line;
    usage("Invalid parameter file line '$line'") if not ($line =~ /=/);
    
    my ($name, $value) = split(/\s*=\s*/, $line);
    usage("Invlid name/value pair '$name' => '$value'") if not ($name and $value);
    
    $ret_hash{$name} = $value;
    
  }
  close(FILE);
  
  return %ret_hash;

}



sub strip {
###########################################################
# P: $text
# R: $text with leading and trailing whitespace removed

  my ($text) = @_;

  $text =~ s,^\s*,,;
  $text =~ s,\s*$,,;

  return $text;
}

